
instance DIA_BaalOrun_Exit(C_Info)
{
	npc = GUR_1209_BaalOrun;
	nr = 999;
	condition = DIA_BaalOrun_Exit_Condition;
	information = DIA_BaalOrun_Exit_Info;
	important = 0;
	permanent = 1;
	description = DIALOG_ENDE;
};


func int DIA_BaalOrun_Exit_Condition()
{
	return TRUE;
};

func void DIA_BaalOrun_Exit_Info()
{
	AI_StopProcessInfos(self);
};


var int BaalOrun_Ansprechbar;
var int BaalOrun_Sakrileg;

instance DIA_BaalOrun_NoTalk(C_Info)
{
	npc = GUR_1209_BaalOrun;
	nr = 2;
	condition = DIA_BaalOrun_NoTalk_Condition;
	information = DIA_BaalOrun_NoTalk_Info;
	permanent = 1;
	important = 1;
};


func int DIA_BaalOrun_NoTalk_Condition()
{
	if(Npc_IsInState(self,ZS_Talk) && (BaalOrun_Ansprechbar == FALSE) && (Npc_GetPermAttitude(self,other) != ATT_FRIENDLY))
	{
		return TRUE;
	};
	return FALSE;
};

func void DIA_BaalOrun_NoTalk_Info()
{
	Info_ClearChoices(DIA_BaalOrun_NoTalk);
	Info_AddChoice(DIA_BaalOrun_NoTalk,DIALOG_ENDE,DIA_BaalOrun_NoTalk_ENDE);
	Info_AddChoice(DIA_BaalOrun_NoTalk,"Все в порядке, приятель?",DIA_BaalOrun_NoTalk_Imp);
	Info_AddChoice(DIA_BaalOrun_NoTalk,"Да пребудет с тобой Спящий!",DIA_BaalOrun_NoTalk_Sleeper);
	Info_AddChoice(DIA_BaalOrun_NoTalk,"Привет! Я здесь новенький.",DIA_BaalOrun_NoTalk_Hi);
};

func void DIA_BaalOrun_NoTalk_Hi()
{
	AI_Output(other,self,"DIA_BaalOrun_NoTalk_Hi_15_00");	//Привет! Я здесь новенький!
	AI_Output(self,other,"DIA_BaalOrun_NoTalk_Hi_12_01");	//Оооох...
	BaalOrun_Sakrileg = 1;
};

func void DIA_BaalOrun_NoTalk_Sleeper()
{
	AI_Output(other,self,"DIA_BaalOrun_NoTalk_Sleeper_15_00");	//Да пребудет с тобой Спящий!
	AI_Output(self,other,"DIA_BaalOrun_NoTalk_Sleeper_12_01");	//Оооох...
	BaalOrun_Sakrileg = 1;
};

func void DIA_BaalOrun_NoTalk_Imp()
{
	AI_Output(other,self,"DIA_BaalOrun_NoTalk_Imp_15_00");	//Все в порядке, приятель?
	AI_Output(self,other,"DIA_BaalOrun_NoTalk_Imp_12_01");	//Оооох...
	BaalOrun_Sakrileg = 1;
};

func void DIA_BaalOrun_NoTalk_ENDE()
{
	AI_StopProcessInfos(self);
};


instance DIA_BaalOrun_FirstTalk(C_Info)
{
	npc = GUR_1209_BaalOrun;
	nr = 1;
	condition = DIA_BaalOrun_FirstTalk_Condition;
	information = DIA_BaalOrun_FirstTalk_Info;
	permanent = 0;
	important = 1;
};


func int DIA_BaalOrun_FirstTalk_Condition()
{
	if(Ghorim_KickHarlok == LOG_SUCCESS)
	{
		BaalOrun_Ansprechbar = 1;
		return TRUE;
	};
	return FALSE;
};

func void DIA_BaalOrun_FirstTalk_Info()
{
	AI_Output(self,other,"DIA_BaalOrun_FirstTalk_12_00");	//Я разговаривал с Горимом. Ты оказал одному из наших братьев большую услугу - и поступил мудро.
	AI_Output(self,other,"DIA_BaalOrun_FirstTalk_12_01");	//Именно поэтому ты был избран для выполнения особой миссии.
	AI_Output(self,other,"DIA_BaalOrun_FirstTalk_12_02");	//Кор Галому для его экспериментов срочно понадобился болотник.
	AI_Output(self,other,"DIA_BaalOrun_FirstTalk_12_03");	//Наши сборщики трудятся день и ночь, не покладая рук. Отправляйся к ним и доставь дневной сбор в лабораторию Галома.
	B_GiveXP(XP_BaalOrunTalks);
	B_LogEntry(CH1_GhorimsRelief,"Харлок заменил Горима. Чудеса все-таки иногда происходят.");
	Log_SetTopicStatus(CH1_GhorimsRelief,LOG_SUCCESS);
	Log_CreateTopic(CH1_DeliverWeed,LOG_MISSION);
	Log_SetTopicStatus(CH1_DeliverWeed,LOG_RUNNING);
	B_LogEntry(CH1_DeliverWeed,"Похоже, дело Харлока произвело впечатление на Гуру Идола Орана. Теперь я удостоен чести забирать весь урожай болотника у новичков и отдавать его Кор Галому.");
	BaalOrun_FetchWeed = LOG_RUNNING;
	Info_ClearChoices(DIA_BaalOrun_FirstTalk);
	Info_AddChoice(DIA_BaalOrun_FirstTalk,"...",DIA_BaalOrun_FirstTalk_MuteEnde);
	Info_AddChoice(DIA_BaalOrun_FirstTalk,"Где я могу найти этих сборщиков?",DIA_BaalOrun_FirstTalk_Where);
};

func void DIA_BaalOrun_FirstTalk_Where()
{
	AI_Output(other,self,"DIA_BaalOrun_FirstTalk_Where_15_00");	//Где я могу найти этих сборщиков?
	AI_Output(self,other,"DIA_BaalOrun_FirstTalk_Where_12_01");	//Я еще не позволял тебе задавать вопросы!
	AI_Output(self,other,"DIA_BaalOrun_FirstTalk_Where_12_02");	//Молись Спящему, дабы простил он твой святотатственный поступок! И ступай. Твоя миссия очень важна для всех нас.
	Info_ClearChoices(DIA_BaalOrun_FirstTalk);
	AI_StopProcessInfos(self);
	BaalOrun_Ansprechbar = 0;
};

func void DIA_BaalOrun_FirstTalk_MuteEnde()
{
	Info_ClearChoices(DIA_BaalOrun_FirstTalk);
	AI_StopProcessInfos(self);
	BaalOrun_Ansprechbar = 0;
};


instance DIA_BaalOrun_GotWeed(C_Info)
{
	npc = GUR_1209_BaalOrun;
	nr = 1;
	condition = DIA_BaalOrun_GotWeed_Condition;
	information = DIA_BaalOrun_GotWeed_Info;
	permanent = 0;
	important = 1;
};


func int DIA_BaalOrun_GotWeed_Condition()
{
	if(Viran_Bloodflies == LOG_SUCCESS)
	{
		return TRUE;
	};
	return FALSE;
};

func void DIA_BaalOrun_GotWeed_Info()
{
	AI_Output(self,other,"DIA_BaalOrun_GotWeed_12_00");	//Ты защитил наших сборщиков...
	AI_Output(self,other,"DIA_BaalOrun_GotWeed_12_01");	//Ты не просто доказал, что находишься на нашей стороне, ты проявил себя верным слугой Спящего.
	AI_Output(self,other,"DIA_BaalOrun_GotWeed_12_02");	//Я считаю, что ты достоин носить робу послушника.
	BaalOrun_Ansprechbar = 1;
	Log_CreateTopic(CH1_JoinPsi,LOG_MISSION);
	if(Npc_GetTrueGuild(hero) == GIL_None)
	{
		Log_SetTopicStatus(CH1_JoinPsi,LOG_RUNNING);
	};
	B_LogEntry(CH1_JoinPsi,"Идол Оран назвал меня достойным слугой Спящего, поскольку я защитил других послушников от шершней на болоте.");
	B_GiveXP(XP_ImpressedBaalOrun);
};


instance DIA_BaalOrun_WeedAtKaloms(C_Info)
{
	npc = GUR_1209_BaalOrun;
	nr = 1;
	condition = DIA_BaalOrun_WeedAtKaloms_Condition;
	information = DIA_BaalOrun_WeedAtKaloms_Info;
	permanent = 0;
	description = "Я доставил болотник Кор Галому.";
};


func int DIA_BaalOrun_WeedAtKaloms_Condition()
{
	if(BaalOrun_FetchWeed == LOG_SUCCESS)
	{
		return TRUE;
	};
	return FALSE;
};

func void DIA_BaalOrun_WeedAtKaloms_Info()
{
	AI_Output(other,self,"DIA_BaalOrun_WeedAtKaloms_15_00");	//Я доставил болотник Кор Галому.
	AI_Output(self,other,"DIA_BaalOrun_WeedAtKaloms_12_01");	//Ты хорошо поработал. Вот небольшая награда за твои старания. Возьми.
	AI_Output(self,other,"DIA_BaalOrun_WeedAtKaloms_12_02");	//Это магическое Заклинание Сна. Ты сможешь воспользоваться им только один раз, но я надеюсь, оно сослужит тебе хорошую службу.
	BaalOrun_Ansprechbar = 1;
	B_GiveXP(XP_ReportToBaalOrun);
	CreateInvItem(self,ItArScrollSleep);
	B_GiveInvItems(self,other,ItArScrollSleep,1);
};


instance DIA_BaalOrun_Perm(C_Info)
{
	npc = GUR_1209_BaalOrun;
	nr = 2;
	condition = DIA_BaalOrun_Perm_Condition;
	information = DIA_BaalOrun_Perm_Info;
	permanent = 1;
	description = "Как продвигается добыча болотника?";
};


func int DIA_BaalOrun_Perm_Condition()
{
	if(BaalOrun_FetchWeed == LOG_SUCCESS)
	{
		return TRUE;
	};
	return FALSE;
};

func void DIA_BaalOrun_Perm_Info()
{
	AI_Output(other,self,"DIA_BaalOrun_Perm_15_00");	//Как продвигается добыча болотника?
	AI_Output(self,other,"DIA_BaalOrun_Perm_12_01");	//Каждый из нас трудится на пределе своих сил, и в результате у нас достаточно болотника не только для собственных нужд - мы даже пускаем его на обмен с другими Лагерями.
	AI_Output(self,other,"DIA_BaalOrun_Perm_12_02");	//Это жертва, которую наши послушники с готовностью приносят Спящему.
};

